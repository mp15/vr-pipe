#!/usr/bin/env perl
use strict;
use warnings;

=head1 AUTHOR

Sendu Bala <sb10@sanger.ac.uk>.

=head1 COPYRIGHT AND LICENSE

Copyright (c) 2011-2012 Genome Research Limited.

This file is part of VRPipe.

VRPipe is free software: you can redistribute it and/or modify it under the
terms of the GNU General Public License as published by the Free Software
Foundation, either version 3 of the License, or (at your option) any later
version.

This program is distributed in the hope that it will be useful, but WITHOUT ANY
WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
PARTICULAR PURPOSE. See the GNU General Public License for more details.

You should have received a copy of the GNU General Public License along with
this program. If not, see L<http://www.gnu.org/licenses/>.

=cut

use VRPipe::FrontEnd;

my $frontend = VRPipe::FrontEnd->new(description => "Provide an overview of problems with pipelines that need user intervention to resolve.",
                                     opt_spec => [ [ 'setup=s', 'Show details for this PipelineSetup only (by name or id)', { persistent_object => 'PipelineSetup' } ],
                                                   [ 'retries=i', 'The number of retries the dispatch daemon is set to attempt before giving up (default 3)', { default => 3 } ] ]);

my $retries = $frontend->opts('retries');
my $setup = $frontend->get_pipelinesetups();

my @subs = VRPipe::Submission->search({ "_failed" => 1, retries => $retries,
                                        $setup ? ('stepstate.pipelinesetup' => $setup->id) : () },
                                      { $setup ? (join => 'stepstate') : (),
                                        prefetch => { stepstate => ['pipelinesetup', { stepmember => 'step' }] } } );

my %summary;
foreach my $sub (@subs) {
    my $state = $sub->stepstate;
    my $setup = $state->pipelinesetup;
    my $setup_name = $setup->name;
    
    my $stepm = $state->stepmember;
    my $step_name = $stepm->step->name;
    
    $summary{$setup->id.'|[]|'.$setup_name}->{$stepm->step_number.'|[]|'.$step_name}++;
}

my $key_sort = sub { my ($ay) = split('\|\[\]\|', $a); my ($be) = split('\|\[\]\|', $b); $ay <=> $be  };
if (keys %summary) {
    foreach my $setup (sort $key_sort keys %summary) {
        my ($setup_id, $setup_name) = split('\|\[\]\|', $setup);
        $frontend->output("---\nPipeline Setup '$setup_name' (id $setup_id) has the following problems:");
        
        my $steps = $summary{$setup};
        foreach my $step (sort $key_sort keys %{$steps}) {
            my ($step_id, $step_name) = split('\|\[\]\|', $step);
            my $fails = $steps->{$step};
            my $cmd = "vrpipe-submissions --setup $setup_id --step '$step_name' --failed";
            $cmd .= ' --deployment testing' if ($frontend->opts('deployment') eq 'testing');
            $frontend->output("\t", "Step $step_name has failed $fails submissions; investigate by running: $cmd");
        }
        
        $frontend->output("---\n\n");
    }
}
else {
    if ($setup) {
        $frontend->output("There are currently no problems with setup ", $setup->id, " requiring manual intervention");
    }
    else {
        $frontend->output("There are currently no problems with anyone's pipelines requiring manual intervention");
    }
}

exit;