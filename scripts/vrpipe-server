#!/usr/bin/env perl
use strict;
use warnings;

=head1 AUTHOR

Sendu Bala <sb10@sanger.ac.uk>.

=head1 COPYRIGHT AND LICENSE

Copyright (c) 2012 Genome Research Limited.

This file is part of VRPipe.

VRPipe is free software: you can redistribute it and/or modify it under the
terms of the GNU General Public License as published by the Free Software
Foundation, either version 3 of the License, or (at your option) any later
version.

This program is distributed in the hope that it will be useful, but WITHOUT ANY
WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
PARTICULAR PURPOSE. See the GNU General Public License for more details.

You should have received a copy of the GNU General Public License along with
this program. If not, see L<http://www.gnu.org/licenses/>.

=cut

use EV;
use AnyEvent;
use AnyEvent::Util qw(fork_call);
use VRPipe::Interface::CmdLine;
use VRPipe::Interface::BackEnd;
use VRPipe::LocalScheduler;
use Sys::CPU;
use POSIX qw(floor);
use List::Util 'shuffle';

# handle options
my $cmdline = VRPipe::Interface::CmdLine->new(
    description => "Control the VRPipe server, which provides the web and cmdline interfaces, the local scheduler, and keeps the pipeline system itself running.",
    extra_args  => 'start|stop|restart|status',
    opt_spec    => [
        ['foreground|f',          'Do not daemonize - the server will run in the foreground, logging to STDERR'],
        ['farm=s',                'Discover and dispatch submissions to the job scheduler identified by the supplied name (only 1 server can dispatch to each farm)'],
        ['max_submissions|m=i',   'The maximum number of submissions to dispatch to the farm when in --farm mode (default is unlimited)'],
        ['only_specified_setups', 'In --farm mode, normally any PipelineSetups that have no farm configured will be run on a random farm, so potentially via this server; turning this option on means this server will only handle setups configured for this farm']
    ]
);

my $backend;
my $deployment            = $cmdline->opts('deployment');
my $foreground            = $cmdline->opts('foreground');
my $farm                  = $cmdline->opts('farm');
my $max_subs              = $cmdline->opts('max_submissions');
my $only_specified_setups = $cmdline->opts('only_specified_setups') || 0;

my $command = shift;
unless ($command) {
    $cmdline->error("a command is required");
    $cmdline->help;
}

# act on the given command
my $status = $cmdline->check_server(1);
my (undef, $port, $url) = @{ $cmdline->_ua_port_baseurl };
my $exit = 0;
if ($command eq 'status') {
    if ($status == 1) {
        $cmdline->output("VRPipe $deployment server is running at $url");
    }
    elsif ($status == 0) {
        $cmdline->output("VRPipe $deployment server is not running");
    }
    elsif ($status == -1) {
        $cmdline->output("A server is listening on port $port, but is not responding like our own VRPipe $deployment server would");
    }
}
elsif ($command eq 'stop') {
    my $stopped = stop();
    $exit = !$stopped;
}
elsif ($command eq 'start') {
    my $started = start();
    $exit = !$started;
}
elsif ($command eq 'restart') {
    $cmdline->output("Restarting VRPipe $deployment server...");
    my $stopped = stop();
    my $started = 0;
    if ($stopped) {
        my $started = start();
    }
    $exit = !$started;
}
else {
    $cmdline->die_with_error("$command is an invalid command");
}

exit $exit;

sub stop {
    if ($status == 1) {
        my $response = $cmdline->server_get('/stop');
        if ($response eq 'Stopping server') {
            # the server responded that it would stop; give it up to 5 seconds
            # to actually do that
            my $seconds = 5;
            while ($seconds--) {
                $status = $cmdline->check_server(1);
                last if $status == 0;
                sleep(1);
            }
            
            if ($status == 0) {
                $cmdline->output("VRPipe $deployment server was stopped");
                return 1;
            }
            else {
                $cmdline->output("VRPipe $deployment server claimed it would stop, but it still seems to be running");
                return 0;
            }
        }
        else {
            $cmdline->output("Failed to stop VRPipe $deployment server (it responded: '$response')");
            return 0;
        }
    }
    elsif ($status == 0) {
        $cmdline->output("VRPipe $deployment server was not running");
        return 1;
    }
    elsif ($status == -1) {
        $cmdline->output("Will not attempt to stop the VRPipe $deployment server listening on port $port, since it does not seem to be ours");
        return 0;
    }
}

sub start {
    if ($status == 1) {
        $cmdline->output("VRPipe $deployment server is already running");
        return 1;
    }
    elsif ($status == 0) {
        $cmdline->output("Will start VRPipe $deployment server at $url");
        start_server();
    }
    elsif ($status == -1) {
        $cmdline->output("Will not attempt to start the VRPipe $deployment server, since another incompatible server is already listening on port $port");
        return 0;
    }
}

sub start_server {
    # initialize the VRPipe backend
    $backend = VRPipe::Interface::BackEnd->new(deployment => $deployment);
    my $dtf = $backend->schema->storage->datetime_parser;
    
    # daemonize unless we're not supposed to
    $backend->daemonize unless $foreground;
    
    $AnyEvent::Util::MAX_FORKS = Sys::CPU::cpu_count();
    my $timer_interval = $deployment eq 'production' ? 30 : 5;
    
    my $watcher_error_sub = sub {
        my $err = $@ || return;
        
        # when testing, we likely start the server and call some database-
        # requiring method before the VRPipe database has been created,
        # so we make a nicer error message
        if (ref($err)) {
            $err = "$err"; # try and stringify it
        }
        my $email_admin = 1;
        if ($err =~ /no such table/) {
            $err         = 'VRPipe database not created yet, cannot do all functions';
            $email_admin = 0;
        }
        $backend->log($err, email_admin => $email_admin);
    };
    
    # set up the timer for the local scheduler to process its queue. (for timers
    # we use periodic instead of timer because timer seems to have some bug
    # where on the first invocation it happens twice at ~the same time, which
    # can be terrible for us. periodic (usually) does not)
    my $ls = VRPipe::LocalScheduler->new;
    my $ls_watcher = EV::periodic_ns 0, $timer_interval, 0, sub {
        # we want to run process_queue every x seconds, but it may take
        # longer than x seconds to run and we don't want to wait, so we do
        # our own async fork (in order to call something that in turn will
        # do further async forks...). It doesn't seem to matter that our
        # children (presumably) have all our watchers running.
        fork_call { $ls->process_queue; return; } $watcher_error_sub;
    };
    
    # setup the submission management system if we're supposed to
    my ($farm_server, $farm_watcher, $handler_path, $scheduler, $still_working);
    my $fork_alarm_time = $deployment eq 'production' ? 3600 : 900;
    my $survival_time;
    if ($farm) {
        # every 30s we'll update which PipelineSetups we'll handle, and also
        # submit handlers to the farm and assign submissions to those handlers
        $farm_watcher = EV::periodic 0, $timer_interval, 0, sub {
            return if $still_working;
            my $t_start = time();
            
            unless ($farm_server) {
                # ideally we only want to be in this if ($farm) section if we
                # have managed to register_farm_server(), but we can only do
                # that once the database is ready, which won't be the case
                # whilst testing. So we use our timer to keep trying to get the
                # farm_server whilst it fails due to database issues, but cancel
                # the timer if we fail because another server is handling this
                # farm
                eval { $farm_server = $backend->register_farm_server($farm, only_ours => $only_specified_setups) };
                if ($@) {
                    &{$watcher_error_sub};
                }
                elsif ($farm_server) {
                    # our database must be ready now, so load all Pipeline and
                    # Step .pm modules into db if we haven't already. we do this
                    # async since if it takes too long the farm_server will
                    # think it died and we'll immediately lose it!
                    unless ($deployment eq 'testing') {
                        fork_call {
                            $backend->install_pipelines_and_steps;
                            return;
                        }
                        $watcher_error_sub;
                    }
                    
                    $backend->log("Submission management started for farm $farm");
                    $handler_path = $cmdline->vrpipe_script_command('vrpipe-handler', $deployment);
                    $handler_path =~ s/^\S+perl/perl/; # different nodes may have different perls installed at different locations
                    $scheduler     = VRPipe::Scheduler->create();
                    $survival_time = $farm_server->survival_time;
                    
                    # start the local scheduler if we're supposed to be using
                    # the local scheduler
                    if ($scheduler->type eq 'local') {
                        $ls_watcher->start;
                        
                        # we need at least 2 forks for this to work though
                        if ($AnyEvent::Util::MAX_FORKS == 1) {
                            $AnyEvent::Util::MAX_FORKS = 2;
                        }
                    }
                }
                else {
                    $backend->log("Could not start submission management for farm $farm because some other server on a different machine is doing that");
                    undef $farm_server;
                }
            }
            else {
                $still_working = 1;
                fork_call {
                    use Time::HiRes qw(gettimeofday tv_interval);
                    my %times;
                    my %elapsed;
                    
                    my $start_clock = sub {
                        my $l1 = shift;
                        $times{$l1} = [gettimeofday];
                        return $l1;
                    };
                    
                    my $elapsed = sub {
                        my ($l1, $l2, $silent) = @_;
                        my $e = sprintf("%0.2f", tv_interval($times{$l1}));
                        my $id = "$l1..$l2";
                        warn "$$ elap_stats line $id took $e seconds\n" unless $silent;
                        push(@{ $elapsed{$id} }, $e);
                    };
                    
                    my $report = sub {
                        my %cums;
                        while (my ($id, $times) = each %elapsed) {
                            my $cum = 0;
                            foreach my $t (@$times) {
                                $cum += $t;
                            }
                            $cums{$id} = $cum;
                        }
                        
                        foreach my $id (sort { $cums{$b} <=> $cums{$a} || $a cmp $b } keys %cums) {
                            my $cum = $cums{$id};
                            
                            my $times = $elapsed{$id};
                            my $total = 0;
                            my $count = 0;
                            foreach my $t (@$times) {
                                $count++;
                                $total += $t;
                            }
                            my $avg = sprintf("%0.2f", $total / $count);
                            
                            my $note = "\t$$ elap_stats $id: $cum seconds";
                            if ($count > 1) {
                                $note .= " ($avg avg over $count loops)\n";
                            }
                            else {
                                $note .= "\n";
                            }
                            warn $note;
                        }
                    };
                    
                    # this fork_call should not take that long, but it is
                    # critical that it not get stuck, or the server will seem to
                    # still be running fine yet won't be submitting any jobs. So
                    # we set up an alarm to kill it after a while
                    local $SIG{ALRM} = sub { die "Taking suspiciously long to ensure that the handlers are running; is the VRPipe database running OK, and is the job scheduler responsive?\n" };
                    alarm $fork_alarm_time;
                    
                    # check we're still valid
                    die "no longer in db\n" unless ($farm_server && $farm_server->alive);
                    $farm_server->stop_beating; # parent process is beating
                    
                    # claim our setups
                    my $l      = &$start_clock(__LINE__);
                    my @setups = $farm_server->claim_setups;
                    &$elapsed($l, __LINE__);
                    my $elap = time() - $t_start;
                    $backend->log("it took ${elap}s to do claim setups");
                    
                    if (@setups) {
                        # make sure we have a setups handler running for our
                        # farm
                        my $shcmd = "$handler_path --mode setups --farm $farm";
                        $backend->log("will ensure the setups handler is running");
                        $l = &$start_clock(__LINE__);
                        $scheduler->ensure_running(
                            cmd          => $shcmd,
                            requirements => VRPipe::Requirements->create(memory => 1500, time => 3600)
                        );
                        &$elapsed($l, __LINE__);
                        # (in production this submits a job to the scheduler
                        #  with a name like *9762b9ae16282af15a8d855bd55c8fa5*)
                        $elap = time() - $t_start;
                        $backend->log("it took ${elap}s to ensure the setups handler was running");
                        
                        # make sure we have sufficient submission handlers
                        # running...
                        
                        # get the count of requirements. Sadly we can't do a
                        # quick cursor->all because we need to take into account
                        # step limits. Also, to avoid handlers running a slow,
                        # complex query and possibly hitting transaction lock/
                        # database update inconsistency issues, instead of them
                        # selecting a Submission to run we will assign a
                        # a Submission, communicating via SidToSub objects; here
                        # we just note the submissions we want to assign
                        my $sched_id = $scheduler->id;
                        $l = &$start_clock(__LINE__);
                        my $sub_pager = VRPipe::Submission->search_paged({ '_done' => 0, -or => [-and => ['_failed' => 1, retries => { '<' => 3 }], '_failed' => 0], scheduler => $sched_id, 'pipelinesetup.controlling_farm' => $farm, 'pipelinesetup.active' => 1, 'dataelement.withdrawn' => 0 }, { join => { stepstate => ['pipelinesetup', 'dataelement'] }, prefetch => ['requirements', 'job', { stepstate => { stepmember => 'step' } }] });
                        &$elapsed($l, __LINE__);
                        
                        my (%req_counts, %step_limits, %step_counts, %completed_setups, %subs_to_assign);
                        my $pager_loop_count = 0;
                        $l = &$start_clock(__LINE__);
                        while (my $subs = $sub_pager->next(no_resetting => 1)) {
                            foreach my $sub (@$subs) {
                                $pager_loop_count++;
                                # if the job is a block_and_skip_if_ok job, we
                                # don't actually block because of race condition
                                # issues, and because of fail and restart
                                # issues. Instead we always only actually queue
                                # if this submission is the first incomplete
                                # submission created for this $job.
                                my $job = $sub->job;
                                if ($job->block_and_skip_if_ok) {
                                    my $m = &$start_clock(__LINE__);
                                    my ($first_sub) = VRPipe::Submission->search({ 'job' => $job->id, '_done' => 0 }, { rows => 1, order_by => { -asc => 'id' } });
                                    &$elapsed($m, __LINE__);
                                    next unless ($first_sub && $first_sub->id == $sub->id);
                                }
                                
                                # global step limit handling
                                my $step    = $sub->stepstate->stepmember->step;
                                my $step_id = $step->id;
                                my $step_limit;
                                if (exists $step_limits{$step_id}) {
                                    $step_limit = $step_limits{$step_id};
                                }
                                else {
                                    # figure out what the step limit is, if any,
                                    # by checking the config of every incomplete
                                    # & active setup that uses the step
                                    my $m = &$start_clock(__LINE__);
                                    my @setups = VRPipe::PipelineSetup->search({ active => 1, 'step.id' => $step_id }, { join => { 'pipeline' => { 'stepmembers' => 'step' } } });
                                    &$elapsed($m, __LINE__);
                                    if (@setups) {
                                        my $step_name = $step->name;
                                        foreach my $setup (@setups) {
                                            unless (exists $completed_setups{ $setup->id }) {
                                                $completed_setups{ $setup->id } = $setup->currently_complete;
                                            }
                                            next if $completed_setups{ $setup->id };
                                            my $setup_limit = $setup->options->{ $step_name . '_max_simultaneous' } || next;
                                            if (!defined $step_limit || $setup_limit < $step_limit) {
                                                $step_limit = $setup_limit;
                                            }
                                        }
                                    }
                                    if (!defined $step_limit) {
                                        # also check the step itself for a limit
                                        $step_limit = $step->max_simultaneous;
                                    }
                                    $step_limits{$step_id} = $step_limit || 0;
                                    
                                    if ($step_limit) {
                                        # see how many of this step are currently
                                        # running, globally
                                        my $m = &$start_clock(__LINE__);
                                        $step_counts{$step_id} = VRPipe::Submission->search({ 'stepmember.step' => $step_id, '_done' => 0, '_failed' => 0, 'job.heartbeat' => { '>' => DateTime->from_epoch(epoch => time() - $survival_time) } }, { join => ['job', { stepstate => 'stepmember' }] });
                                        &$elapsed($m, __LINE__);
                                    }
                                }
                                if ($step_limit) {
                                    next if $step_counts{$step_id} >= $step_limits{$step_id};
                                }
                                
                                # we'll want to make sure there'll be a
                                # handler for this one
                                my $req_id = $sub->requirements->id;
                                $req_counts{$req_id}++;
                                $step_counts{$step_id}++;
                                
                                # we'll want to assign this one
                                $subs_to_assign{$req_id}->{ $sub->id } = 1;
                            }
                        }
                        &$elapsed($l, __LINE__);
                        $elap = time() - $t_start;
                        $backend->log("it took ${elap}s to do the pager loop ($pager_loop_count subs)");
                        
                        # scale the counts according to $max_subs
                        #*** and eventually, we should have a priority system
                        #    where a particular setup can have higher priority
                        #    than others...
                        if ($max_subs) {
                            my $total_count = 0;
                            foreach my $count (values %req_counts) {
                                $total_count += $count;
                            }
                            
                            if ($total_count > $max_subs) {
                                my $reducer = $max_subs / $total_count;
                                while (my ($req_id, $count) = each %req_counts) {
                                    $req_counts{$req_id} = floor($count * $reducer);
                                    $req_counts{$req_id} ||= 1;
                                }
                            }
                        }
                        
                        # submit the submission handlers to the scheduler
                        my %running_in_scheduler;
                        $l = &$start_clock(__LINE__);
                        while (my ($req_id, $count) = each %req_counts) {
                            my $cmd = "$handler_path --mode submissions --farm $farm -r $req_id -s $sched_id";
                            
                            $backend->log("will ensure that $count sub handlers are running for [$cmd]");
                            my $m            = &$start_clock(__LINE__);
                            my @running_sids = $scheduler->ensure_running(
                                cmd          => $cmd,
                                count        => $count,
                                requirements => VRPipe::Requirements->get(id => $req_id),
                            );
                            &$elapsed($m, __LINE__);
                            
                            $m = &$start_clock(__LINE__);
                            foreach my $sid_aid (@running_sids) {
                                $running_in_scheduler{$sid_aid} = 1;
                            }
                            &$elapsed($m, __LINE__);
                        }
                        &$elapsed($l, __LINE__);
                        $elap = time() - $t_start;
                        $backend->log("it took ${elap}s to submit the submission handlers");
                        
                        # assign sub_ids to sidtosubs
                        $l = &$start_clock(__LINE__);
                        my %already_assigned = map { $_ => 1 } VRPipe::SidToSub->get_column_values('sub_id', { farm => $farm, sub_id => { '!=' => undef } });
                        &$elapsed($l, __LINE__);
                        while (my ($req_id, $sub_hash) = each %subs_to_assign) {
                            $subs_to_assign{$req_id} = [shuffle grep { !exists $already_assigned{$_} } keys %{$sub_hash}];
                        }
                        $l = &$start_clock(__LINE__);
                        foreach my $sidtosub (VRPipe::SidToSub->search({ farm => $farm, sub_id => undef })) {
                            my $req_id = $sidtosub->req_id;
                            my $sub_id = pop @{ $subs_to_assign{$req_id} || [] } || next;
                            $backend->log("assigning sidtosub " . $sidtosub->id . " sub $sub_id for req $req_id");
                            $sidtosub->sub_id($sub_id);
                            $sidtosub->update;
                        }
                        &$elapsed($l, __LINE__);
                        $elap = time() - $t_start;
                        $backend->log("it took ${elap}s to assign sub_ids to sidtosubs");
                        
                        # delete any old defunct or stuck SidToSubs, and kill
                        # them from the scheduler as well
                        my $leeway = 300;
                        my @sids_to_kill;
                        my $rows_deleted      = 0;
                        my $sidtosubs_checked = 0;
                        $l = &$start_clock(__LINE__);
                        foreach my $sidtosub (VRPipe::SidToSub->search({ farm => $farm })) {
                            $sidtosubs_checked++;
                            $backend->log("checking if sidtosub " . $sidtosub->id . " is defunct");
                            my $sid = $sidtosub->sid;
                            my $aid = $sidtosub->aid;
                            if (exists $running_in_scheduler{"$sid\[$aid]"}) {
                                # check that it has not been longer than 5mins
                                # between sid_id being assigned and the sub's
                                # job starting
                                $backend->log("it is currently running");
                                my $sub_id = $sidtosub->sub_id;
                                
                                if ($sub_id) {
                                    $backend->log("it has been assigned a sub_id");
                                    my $m = &$start_clock(__LINE__);
                                    my ($sub) = VRPipe::Submission->search({ 'me.id' => $sub_id }, { prefetch => 'job' });
                                    &$elapsed($m, __LINE__);
                                    if ($sub) {
                                        # if sub done/failed, the handler will soon
                                        # unset sub_id and we want it to hang around
                                        # until we assign it a new one
                                        next if ($sub->done || $sub->failed);
                                        $backend->log("that sub isn't done yet");
                                        
                                        # if the job has started and is running, all
                                        # is well
                                        my $tsh = $sub->job->time_since_heartbeat;
                                        next if $tsh && $tsh <= $leeway;
                                        $backend->log("the job is dead or hasn't started yet");
                                        
                                        next if $sidtosub->time_since_assignment <= $leeway;
                                        $backend->log("it's been over 5mins since assignment");
                                    }
                                }
                                else {
                                    # it hasn't been assigned a sub_id yet, but
                                    # is that because we don't need a handler
                                    # for this req_id anymore?
                                    my $req_id = $sidtosub->req_id;
                                    next if (defined $subs_to_assign{$req_id} && ref($subs_to_assign{$req_id}) eq 'ARRAY' && @{ $subs_to_assign{$req_id} });
                                    $backend->log("it has not been assigned a sub_id because we no longer need a handler for req $req_id");
                                }
                                
                                $backend->log("we'll kill sid $sid $aid");
                                push(@sids_to_kill, [$sid, $aid]);
                            }
                            else {
                                # we might not know it's running because
                                # %running_in_scheduler is only populated for
                                # req_ids we still needed to run, so check and
                                # kill as appropriate. Theoretically just
                                # deleting the sidtosub should make it kill
                                # itself, but I don't want to rely on that
                                my $req_id = $sidtosub->req_id;
                                unless (defined $req_counts{$req_id}) {
                                    $backend->log("we'll kill sid $sid $aid because it's req $req_id is no longer needed");
                                    push(@sids_to_kill, [$sid, $aid]);
                                }
                            }
                            
                            # if we got this far it means either we're running
                            # with an assigned sub but aren't doing anything
                            # with it (we are stuck), or we're not running so
                            # must have exited
                            $backend->log("we'll delete the sidtosub");
                            $sidtosub->delete;
                            $rows_deleted++;
                        }
                        &$elapsed($l, __LINE__);
                        $elap = time() - $t_start;
                        $backend->log("it took ${elap}s to find defunct sids and sidtosubs ($rows_deleted deleted of $sidtosubs_checked checked)");
                        
                        # kill anything running in the scheduler for which there
                        # has not been a sidtosub created for over 5 mins
                        $l = &$start_clock(__LINE__);
                        foreach my $sid_aid (keys %running_in_scheduler) {
                            my ($sid, $aid) = $sid_aid =~ /(\d+)\[(\d+)/;
                            my $m = &$start_clock(__LINE__);
                            my $created = VRPipe::SidToSub->search({ farm => $farm, sid => $sid, aid => $aid });
                            &$elapsed($m, __LINE__);
                            unless ($created) {
                                my $run_time = $scheduler->run_time($sid, $aid);
                                $backend->log("sid $sid $aid has been running for $run_time seconds");
                                if ($run_time > $leeway) {
                                    $backend->log("we'll kill sid $sid $aid because it never created a sidtosub");
                                    push(@sids_to_kill, [$sid, $aid]);
                                }
                            }
                            else {
                                $backend->log("sid $sid $aid has $created sidtosubs");
                            }
                        }
                        &$elapsed($l, __LINE__);
                        $elap = time() - $t_start;
                        $backend->log("it took ${elap}s to find sids that never created a sidtosub");
                        
                        # batch kill sids now; this should hopefully be faster
                        # than calling kill_sid() for each sid in the above
                        # loops
                        $l = &$start_clock(__LINE__);
                        $scheduler->batch_kill_sids(\@sids_to_kill);
                        &$elapsed($l, __LINE__);
                        $elap = time() - $t_start;
                        $backend->log("it took ${elap}s to batch kill sids (@sids_to_kill)");
                    }
                    
                    &$report();
                    
                    alarm 0;
                    $elap = time() - $t_start;
                    $backend->log("it took ${elap}s to run the fork call");
                    return;
                }
                sub {
                    $still_working = 0;
                    my $err = $@;
                    if ($err) {
                        # perhaps another process decided our $farm_server was
                        # no longer alive and it has been deleted from the db?
                        # let's still keep the farmserver watcher alive so we
                        # will try and re-register as a farmserver - necessary
                        # when running a bunch of tests where the database keeps
                        # getting dropped and recreated while the server is
                        # running
                        if ($err =~ /no longer in db/) {
                            $backend->log("Could not continue submission management for farm $farm because our entry was removed from the FarmServer tracking table.", email_admin => 1);
                            $farm_server->end_it_all if $farm_server;
                            undef $farm_server;
                            $ls_watcher->stop;
                        }
                        &{$watcher_error_sub};
                    }
                };
            }
        };
    }
    
    # set up the http page requests we'll respond to
    $backend->register_psgi_pages(
        '/' => sub {
            $backend->psgi_text_response(200, 'html', "<html><body><h1>VRPipe Homepage</h1>" . q[<p>Complete web interface to VRPipe coming soon; currently you can just look at the <a href="/status?brief=1&amp;incomplete=1">status</a> page (like the vrpipe-status command line script).</p>] . "</body></html>", shift);
        },
        '/status'            => sub { $backend->psgi_nonblocking_xml_response(\&status,          shift); },
        '/local_scheduler'   => sub { $backend->psgi_nonblocking_xml_response(\&local_scheduler, shift, $ls_watcher); },
        '/continuation_test' => sub { $backend->psgi_continuation_response(\&continuation_test,  shift); },
        '/dsn' => sub {
            $backend->psgi_text_response(200, 'plain', $backend->dsn, shift);
        },
        '/stop' => sub {
            shutdown_server("Received the stop command");
            $backend->psgi_text_response(200, 'plain', 'Stopping server', shift);
        }
    );
    
    # set up the signals we'll react to
    $SIG{HUP}  = 'IGNORE';
    $SIG{PIPE} = 'IGNORE';
    my $sigterm_watcher = EV::signal 'TERM', sub { shutdown_server("SIGTERM received"); };
    my $sigint_watcher  = EV::signal 'INT',  sub { shutdown_server("SIGINT received"); };
    my $sigquit_watcher = EV::signal 'QUIT', sub { shutdown_server("SIGQUIT received"); };
    
    # run the event loop
    $backend->log("The $deployment server at $url for VRPipe database " . $backend->dsn . " has started for farm $farm (pid $$).", email_admin => 1, subject => 'The VRPipe Server is alive!') if $farm;
    EV::run;
    
    $backend->log("The $deployment server at $url for VRPipe database " . $backend->dsn . " ($farm farm) is exiting!", email_admin => 1, subject => 'The VRPipe Server is dead!') if $farm;
    exit 0;
}

sub shutdown_server {
    my $msg = shift;
    $backend->log($msg . ", will gracefully shut down server.");
    EV::unloop;
}

# "page" subs
sub status {
    my $req = shift;
    
    my $opts = $backend->req_to_opts($req, ['setup!PipelineSetup']);
    $opts->{'_multiple_setups'} = 1;
    my $list            = $opts->{list};
    my $incomplete_only = $opts->{incomplete};
    my $brief           = $opts->{brief};
    my $defunct_only    = $opts->{defunct};
    if ($defunct_only) {
        undef $list;
        undef $incomplete_only;
        undef $brief;
        $opts->{deactivated} = 1;
    }
    
    my $xml = '<title>Current status of PipelineSetups</title>';
    
    # $xml .= '<options><bool></bool></options>';
    
    my @setups = $backend->get_pipelinesetups($opts);
    if (@setups) {
        my @objects;
        foreach my $setup (@setups) {
            my $object;
            foreach my $attr (qw(id name user active)) {
                $object .= $backend->xml_tag('attribute', $setup->$attr(), qq[name="$attr"]);
            }
            
            if ($list) {
                push(@objects, $object);
                next;
            }
            
            my $datasource = $setup->datasource;
            my $num_elements;
            my $incomplete_pager;
            my $ps_id = $setup->id;
            eval {
                $num_elements = VRPipe::DataElementState->search({ pipelinesetup => $setup->id, 'dataelement.datasource' => $datasource->id, 'dataelement.withdrawn' => 0 }, { join => 'dataelement' });
                $incomplete_pager = $datasource->incomplete_element_states($setup, prepare => 0);
            };
            my $ds_error = $@;
            if ($ds_error || !$incomplete_pager) {
                $ds_error ||= 'Could not create an incomplete_element_states pager for setup ' . $setup->id;
                if ($defunct_only) {
                    #*** due to prepare => 0 above, this will never happen, but
                    #    to prepare => 1 in $defunct_only mode would be too slow
                    my $prob = $ds_error ? 'a bad datasource' : 'no dataelements';
                    $object .= $backend->xml_tag('attribute', "This has $prob; investigate with [vrpipe-status --setup $ps_id] and consider deleting it with [vrpipe-setup --setup $ps_id --delete]", qq[name="problems"]);
                }
                else {
                    $object .= $backend->xml_tag('attribute', "There is a problem with the DataSource for this pipeline, so no more information can be retrieved about it.\n$ds_error\n", qq[name="problems"]);
                }
                push(@objects, $object);
                next;
            }
            
            my $num_incomplete = $incomplete_pager->total_entries;
            next if $incomplete_only && !$num_incomplete;
            
            if ($defunct_only) {
                if ($num_incomplete && $setup->active == 0) {
                    $object .= $backend->xml_tag('attribute', "This is inactive but has $num_incomplete incomplete dataelements; if this setup was started but then abandoned (due to an error setting it up, or it being replaced by another setup) consider deleting it with [vrpipe-setup --setup $ps_id --delete]", qq[name="problems"]);
                    push(@objects, $object);
                }
                next;
            }
            
            my $pipeline     = $setup->pipeline;
            my $step_members = $pipeline->step_members;
            unless ($brief) {
                # pipeline details
                $object .= '<attribute name="pipeline"><object class="Pipeline" display_mode="full">';
                foreach my $attr (qw(id name description)) {
                    $object .= $backend->xml_tag('attribute', $pipeline->$attr(), qq[name="$attr"]);
                }
                $object .= $backend->xml_tag('attribute', $step_members, q[name="num_steps"]);
                $object .= '</object></attribute>';
                
                # setup options and output root
                my $ps_opts = $setup->options;
                $object .= '<attribute name="options">';
                if (keys %$ps_opts) {
                    $object .= $backend->hash_to_xml($ps_opts);
                }
                else {
                    $object .= '(used with default/no options)';
                }
                $object .= '</attribute>';
                $object .= $backend->xml_tag('attribute', $setup->output_root->stringify, q[name="output_root"]);
                
                # datasource details
                $object .= '<attribute name="datasource"><object class="DataSource" display_mode="full">';
                foreach my $attr (qw(id type method source)) {
                    $object .= $backend->xml_tag('attribute', $datasource->$attr(), qq[name="$attr"]);
                }
                my $ds_opts = $datasource->options;
                $object .= '<attribute name="options">';
                if (keys %$ds_opts) {
                    $object .= $backend->hash_to_xml($ds_opts);
                }
                else {
                    $object .= '(used with default/no options)';
                }
                $object .= '</attribute>';
                $object .= '</object></attribute>';
            }
            
            $object .= $backend->xml_tag('attribute', $num_elements,   q[name="elements_total"]);
            $object .= $backend->xml_tag('attribute', $num_incomplete, q[name="elements_incomplete"]);
            if ($num_incomplete) {
                my %status_hash;
                my $fully_complete = $num_elements - $num_incomplete;
                $status_hash{$step_members} = $fully_complete;
                my $num_steps_complete = $fully_complete * $step_members;
                while (my $incompletes = $incomplete_pager->next) {
                    foreach my $es (@$incompletes) {
                        my $completed_steps = $es->completed_steps;
                        $num_steps_complete += $completed_steps;
                        $status_hash{$completed_steps}++;
                    }
                }
                $object .= '<attribute name="steps_completed">' . $backend->hash_to_xml(\%status_hash, [sort { my ($ay) = $a =~ /^(\d+)/; my ($be) = $b =~ /^(\d+)/; $ay <=> $be } keys %status_hash]) . '</attribute>';
                
                my $steps_to_complete = $num_elements * $step_members;
                my $percent_complete = sprintf("%0.2f", (100 / $steps_to_complete) * $num_steps_complete);
                $object .= $backend->xml_tag('attribute', "$percent_complete\%", qq[name="completion" explanation="Pipeline currently $percent_complete\% complete ($num_steps_complete / $steps_to_complete steps completed)"]);
                
                my $farm = $setup->controlling_farm;
                if ($farm) {
                    my ($farm_server) = VRPipe::FarmServer->search({ farm => $farm });
                    if (!$farm_server || !$farm_server->alive) {
                        $object .= $backend->xml_tag('attribute', "WARNING! setup may be stalled because it is being controlled by the VRPipe server for farm $farm, but no server for that farm is currently alive", q[name="problems"]);
                    }
                }
                
                my $pager = VRPipe::Submission->search_paged({ "_done" => 0, 'stepstate.pipelinesetup' => $setup->id, 'dataelement.withdrawn' => 0 }, { join => { 'stepstate' => 'dataelement' }, prefetch => 'job' });
                my %sub_stats;
                while (my $subs = $pager->next(no_resetting => 1)) {
                    foreach my $sub (@$subs) {
                        next if $sub->done;
                        my $sub_state = $sub->failed ? 'failed' : 'scheduled';
                        if ($sub_state eq 'scheduled') {
                            my $job = $sub->job;
                            $sub_state = $job->end_time ? 'processing' : ($job->heartbeat ? 'running' : 'pending');
                        }
                        $sub_stats{$sub_state}++;
                    }
                }
                if (keys %sub_stats) {
                    $object .= '<attribute name="submission_state">' . $backend->hash_to_xml(\%sub_stats, [qw(running processing pending failed)]) . '</attribute>';
                    if (keys %sub_stats == 1 && exists $sub_stats{failed}) {
                        my $failed = $sub_stats{failed};
                        if ($failed >= $num_incomplete) {
                            $object .= $backend->xml_tag('attribute', "WARNING! It looks like this setup may be STALLED due to repeated failures.\nInvestigate using: vrpipe-submissions --setup " . $setup->id . " --failed", q[name="problems"]);
                        }
                    }
                }
                else {
                    # well, what is happening to this setup then? probably we
                    # have the same submissions as some other setup
                    $incomplete_pager = $datasource->incomplete_element_states($setup, prepare => 0);
                    my @step_members  = $pipeline->step_members;
                    my $found_problem = 0;
                    ESCHECKER: while (my $incompletes = $incomplete_pager->next) {
                        foreach my $es (@$incompletes) {
                            my $element         = $es->dataelement;
                            my $completed_steps = $es->completed_steps;
                            next if $completed_steps == $step_members;
                            
                            foreach my $member (@step_members) {
                                my $step_number = $member->step_number;
                                my ($state) = VRPipe::StepState->search({
                                        stepmember    => $member,
                                        dataelement   => $element,
                                        pipelinesetup => $setup
                                    }
                                );
                                next unless $state;
                                next if $state->complete;
                                
                                my @submissions = $state->submissions;
                                if (@submissions) {
                                    my $other_state = $state->same_submissions_as;
                                    if ($other_state) {
                                        my $other_setup = $other_state->pipelinesetup->id;
                                        if ($other_setup != $setup->id && $other_state->complete != 1) {
                                            $object .= $backend->xml_tag('attribute', "Note: this setup is dependent on work being done by setup $other_setup, so check the status of that to see what's happening.");
                                            $found_problem = 1;
                                            last ESCHECKER;
                                        }
                                    }
                                }
                                else {
                                    $object .= $backend->xml_tag('attribute', "Note: currently processing step $step_number (" . $member->step->name . ").\nIt's possible something has gone wrong with this step and the setup is stalled; you should have received an email with further details in that case.");
                                    $found_problem = 1;
                                    last ESCHECKER;
                                }
                            }
                        }
                    }
                    
                    $object .= $backend->xml_tag('attribute', "Note: the setup hasn't completed but there are no submissions for it and I can't detect obvious problems; maybe it's in the middle of processing and something will happen soon.") unless $found_problem;
                }
            }
            else {
                if ($num_elements == 0) {
                    $object .= '<attribute name="completion" explanation="Pipeline has no inputs to work on (yet?), so has done nothing.">n/a</attribute>';
                }
                else {
                    $object .= '<attribute name="completion" explanation="Pipeline currently 100% complete!">100%</attribute>';
                }
            }
            
            push(@objects, $object);
        }
        
        if (@objects) {
            my $mode = $list ? 'list' : ($brief ? 'brief' : ($defunct_only ? 'defunct' : 'full'));
            $xml .= '<objects>' . join('', map { qq[<object class="PipelineSetup" display_mode="$mode">] . $_ . '</object>' } @objects) . '</objects>';
        }
    }
    
    return $xml;
}

sub local_scheduler {
    my ($req, $ls_watcher) = @_;
    
    my $opts = $backend->req_to_opts($req);
    my $mode = $opts->{mode} || die "a mode is required\n";
    my $ids  = $opts->{id};
    my @ids;
    if ($ids) {
        if (ref($ids)) {
            @ids = @$ids;
        }
        else {
            @ids = ($ids);
        }
    }
    
    my $xml = '';
    if ($mode eq 'start') {
        $xml = '<title>Local Scheduler</title>';
        $ls_watcher->start;
        $xml .= '<response_line>Local Scheduler started</response_line>';
    }
    elsif ($mode eq 'stop') {
        $xml = '<title>Local Scheduler</title>';
        $ls_watcher->stop;
        $xml .= '<response_line>Local Scheduler stopped</response_line>';
    }
    elsif ($mode eq 'submit') {
        $xml = '<title>Job Submission to Local Scheduler</title>';
        my $cmd = $opts->{cmd} || die "a cmd is required when submitting\n";
        my $o   = $opts->{out} || die "the out option is required when submitting\n";
        my $e   = $opts->{err} || die "the err option is required when submitting\n";
        my $a   = $opts->{a};
        my $env = $opts->{env} || {};
        my $ls = VRPipe::LocalScheduler->new(o => $o, e => $e, $a ? (a => $a) : ());
        $xml .= $backend->xml_tag('response_line', $ls->submit($cmd, $env));
    }
    elsif ($mode eq 'jobs') {
        $xml = '<title>Jobs in the Local Scheduler</title>';
    }
    elsif ($mode eq 'kill') {
        $xml = '<title>Jobs killed from the Local Scheduler</title>';
        @ids || die "at least 1 id must be supplied\n";
    }
    else {
        die "unknown mode '$mode'\n";
    }
    
    if ($mode eq 'jobs' || $mode eq 'kill') {
        my $ls = VRPipe::LocalScheduler->new;
        my ($lines, $warnings) = $ls->$mode(\@ids);
        foreach my $line (@$warnings) {
            $xml .= $backend->xml_tag('warning', $line);
        }
        foreach my $line (@$lines) {
            $xml .= $backend->xml_tag('response_line', $line);
        }
    }
    
    return $xml;
}

sub continuation_test {
    my $request = shift;
    $request->print("Your name: <form><input type=text name=name></form>");
    $request->next; # this waits for the form to be submitted!
    my $name = $request->param('name');
    $request->print("Hello $name!");
}
